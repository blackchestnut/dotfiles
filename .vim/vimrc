let mapleader = ';'

"-------------------------------------------------------------------------------
" fold
" NOTE: Collapse the method body
"-------------------------------------------------------------------------------
nmap <Backspace> za
set foldenable
set foldlevelstart=10
" syntax folding might not work in some cases (e.g. in spec files)
set foldmethod=indent
set foldnestmax=10

"-------------------------------------------------------------------------------
" Clipboard (unix only)
" NOTE: win32 http://vim.wikia.com/wiki/Copy_filename_to_clipboard
"-------------------------------------------------------------------------------
nmap ,cs :let @*=expand("%")<CR>
nmap ,cl :let @*=expand("%:p")<CR>

"-------------------------------------------------------------------------------
" Pathogen
"-------------------------------------------------------------------------------
filetype off
call pathogen#helptags()
call pathogen#runtime_append_all_bundles()
filetype on

"-------------------------------------------------------------------------------
" NerdTree (NERD)
"-------------------------------------------------------------------------------
let g:NERDCreateDefaultMappings = 0
let g:NERDSpaceDelims = 0
let g:NERDDefaultAlign = 'left'

map <Leader><Space> <Plug>NERDCommenterToggle
map <silent> <leader>n :NERDTreeToggle<cr>
map <silent> <leader>N :NERDTreeFind<cr>

"-------------------------------------------------------------------------------
" NERDCommenter
"-------------------------------------------------------------------------------
map ,<space> <plug>NERDCommenterToggle

"-------------------------------------------------------------------------------
" Syntastic
"-------------------------------------------------------------------------------
nmap <silent> <Leader>cc :SyntasticCheck<CR>
nmap <silent> <Leader>cr :SyntasticReset<CR>

let g:syntastic_javascript_eslint_exe = '$(npm bin)/eslint'
let g:syntastic_javascript_checkers=['eslint']
let g:syntastic_mode_map = {
      \   'mode': 'passive',
      \   'active_filetypes': [],
      \   'passive_filetypes': []
      \ }
let g:syntastic_ruby_mri_exec = '~/.rvm/rubies/ruby-2.3.0/bin/ruby'

" http://vim.wikia.com/wiki/Simplifying_regular_expressions_using_magic_and_no-magic
"
" '\m^shadowing outer local variable'
let g:syntastic_ruby_mri_quiet_messages = {
\   'regex': [
\     '\m`&'' interpreted as argument prefix',
\     '\m`*'' interpreted as argument prefix',
\     '\mambiguous first argument; put parentheses or a space even after `/'' operator'
\   ]
\ }
let g:syntastic_coffee_checkers=['coffee']
let g:syntastic_slim_checkers=['slimrb']
let g:syntastic_json_checkers=['jsonlint'] " npm install -g jsonlint
let g:vim_json_syntax_conceal = 0
let g:syntastic_sass_checkers=[]

"-------------------------------------------------------------------------------
" javascript-libraries-syntax.vim
"-------------------------------------------------------------------------------
let g:used_javascript_libs = 'react,underscore,jquery,d3'

"-------------------------------------------------------------------------------
" Robocop
"-------------------------------------------------------------------------------
let g:vimrubocop_config = '~/.vim/configs/rubocop/default.yml'

"-------------------------------------------------------------------------------
" matchit
"-------------------------------------------------------------------------------
runtime macros/matchit.vim

"-------------------------------------------------------------------------------
" powerline
"-------------------------------------------------------------------------------
let g:airline_powerline_fonts = 1
set laststatus=2

"-------------------------------------------------------------------------------
" Command-T
"-------------------------------------------------------------------------------
let g:CommandTMatchWindowReverse = 0
let g:CommandTMaxHeight = 17
let g:CommandTMaxFiles = 25000
let g:CommandTWildIgnore = &wildignore."*.o,*.obj,.git,.svn,*.png,*.jpg,.DS_Store,*.log,public/uploads/**,public/images/**,public/system/images/**,public/assets/**,tmp/**,client/node_modules,node_modules/**,android/app/build/**,ios/build/**"

nmap <silent> <leader>t :CommandT<cr>
nmap <silent> <leader>r :CommandTFlush<cr>:CommandT<cr>
nmap <silent> <leader>j :CommandTJump<cr>

"-------------------------------------------------------------------------------
" vim-markdown
"-------------------------------------------------------------------------------

let g:vim_markdown_folding_disabled = 1
let g:vim_markdown_frontmatter = 1

"-------------------------------------------------------------------------------
" vim-rails
"
" example projections: https://gist.github.com/henrik/5676109
"-------------------------------------------------------------------------------

nmap <Leader>, :A<cr>
nmap <Leader>v :AV<cr>
nmap gs :AV<cr><c-w><s-l>

let g:rails_projections = {
\   'app/admin/*.rb': {
\     'alternate': 'spec/controllers/admin/{}_controller_spec.rb'
\   },
\   'spec/controllers/admin/*_controller_spec.rb': {
\     'alternate': 'app/admin/{}.rb'
\   },
\   'lib/*.rb': {
\     'alternate': 'spec/{}_spec.rb'
\   },
\   'spec/*_spec.rb': {
\     'alternate': 'lib/{}.rb'
\   },
\   'config/locales/*.ru.yml': {
\     'alternate': 'config/locales/{}.en.yml'
\   },
\   'config/locales/*.en.yml': {
\     'alternate': 'config/locales/{}.ru.yml'
\   }
\ }

"-------------------------------------------------------------------------------
" Tagbar
" https://github.com/majutsushi/tagbar/blob/master/doc/tagbar.txt
"-------------------------------------------------------------------------------
nnoremap <silent> <Leader>b :TagbarToggle<cr>
let g:tagbar_autofocus = 1
let g:tagbar_autoclose = 1
let g:tagbar_map_jump = 'o'
let g:tagbar_map_showproto = '<cr>'
let g:tagbar_map_togglefold = '<space>'
let g:tagbar_type_ruby = {
    \ 'kinds' : [
        \ 'm:modules',
        \ 'c:classes',
        \ 'd:describes',
        \ 'C:contexts',
        \ 'f:methods',
        \ 'F:singleton methods'
    \ ]
\ }
let g:tagbar_type_markdown = {
    \ 'ctagstype' : 'markdown',
    \ 'kinds' : [
        \ 'h:Heading_L1',
        \ 'i:Heading_L2',
        \ 'k:Heading_L3'
    \ ]
\ }

"-------------------------------------------------------------------------------
" QFEnter
"-------------------------------------------------------------------------------

" To open search window in new tab set 1
let g:qfenter_enable_autoquickfix = 0

let g:qfenter_keymap = {}
let g:qfenter_keymap.open = ['<CR>']
let g:qfenter_keymap.open_keep = ['<S-CR>']
let g:qfenter_keymap.hopen = ['<C-s>']
let g:qfenter_keymap.vopen = ['<C-v>']
let g:qfenter_keymap.topen = ['<C-t>']

"-------------------------------------------------------------------------------
" ag.vim
"-------------------------------------------------------------------------------

let g:ag_mapping_message = 0
let g:ag_prg = 'ag --vimgrep --literal'
map <Leader>s :LAg!<Space>

"-------------------------------------------------------------------------------
" options
"-------------------------------------------------------------------------------
set nocompatible
set backup
set nowrap
set history=9999
set undolevels=9999
filetype plugin on
" directory for swap files
set directory=$HOME
" russian language fix
set langmap=ё`,йq,цw,уe,кr,еt,нy,гu,шi,щo,зp,фa,ыs,вd,аf,пg,рh,оj,лk,дl,э',яz,чx,сc,мv,иb,тn,ьm,б\\,,ю.,Ё~,ЙQ,ЦW,УE,КR,ЕT,НY,ГU,ШI,ЩO,ЗP,ФA,ЫS,ВD,АF,ПG,РH,ОJ,ЛK,ДL,Ж:,Э\\",ЯZ,ЧX,СC,МV,ИB,ТN,ЬM,Б<,Ю>
" one word symbol class
set iskeyword=@,48-57,_,192-255,\$,\-,\?
" encodings
set fileencodings=utf-8,windows-1251,iso-8859-15,koi8-r
" fileformat
"set fileformats=dos
" sessions
set sessionoptions=curdir,buffers,tabpages,folds,options,resize,globals,localoptions
"set noeol bin
" keep more context when scrolling off the end of a buffer
" indents
"set cindent
set autoindent    " always set autoindenting on
set copyindent    " copy the previous indentation on autoindenting
set shiftround    " use multiple of shiftwidth when indenting with '<' and '>'
                  "    shiftwidth, not tabstop
set nojoinspaces
"set smartindent   " ломает отступы в случае 'if foo(enter)'
set expandtab
set shiftwidth=2  " number of spaces to use for autoindenting

set softtabstop=2
set tabstop=2
set softtabstop=2
" search
set incsearch
set hlsearch
set showmatch
" clipboard
"set clipboard=unnamedplus
"set noignorecase
set ignorecase    " ignore case when searching
set smartcase     " ignore case if search pattern is all lowercase,
                  "    case-sensitive otherwise
set smarttab      " insert tabs on the start of a line according to
set noswapfile
" gui
syntax on

"-------------------------------------------------------------------------------
" Scheme
"-------------------------------------------------------------------------------
set t_Co=256

"1. ir_black
"colors ir_black_blackchestnut

"2. PaperColor
set background=light
"set background=dark
colorscheme PaperColor

"-------------------------------------------------------------------------------
" Fonts
"-------------------------------------------------------------------------------

"------------------------------------------------------------------------------80------------------100-------------------------------------140-----------------160
" For text
" 1234567890
" !@#$%^&**()_+=;.:|/?<>
" qwertyuiopasdfghjklzxcvbnm
" QWERTYUIOPASDFGHJKLZXCVBNM

set guifont=Monaco\ for\ Powerline:h14
set linespace=0

"set linespace=-2
"set guifont=Inconsolata\ LGC:h14

"set linespace=3
"set guifont=Inconsolata:h14

"set linespace=2
"set guifont=Input\ Mono\ Narrow:h13

"set linespace=1
"set guifont=Andale\ Mono\ MT\ Std:h13

set number " always show line numbers
set cpoptions+=$
set colorcolumn=84
set guioptions-=T
set ch=1
set noguipty
set nostartofline
set whichwrap+=>
set whichwrap+=<
let c_comment_strings=1
set lazyredraw
set modeline
set scrolloff=3
set sidescrolloff=7
set sidescroll=1
"set guifont=Courier\ New:h12:cRUSSIAN
set confirm
set shortmess=fimnrxoOtTI
set ruler
" ringbell
set novisualbell
set t_vb=
set vb t_vb=
" show unfinished commands in statusbar
set showcmd
set cursorline
"norm \[i
" display current mode
"set showmode
set noshowmode
set noerrorbells
"set nocp " option for cppomnicomplete
set list
"set listchars=trail:.
set listchars=tab:>.,trail:.,extends:#,nbsp:.
" allow to use backspace instead of "x"
set backspace=indent,eol,start whichwrap+=<,>,[,]
" do not abandon buffer when it is unloaded
set hidden
" mouse
set mousemodel=popup
set mouse=a
set mousehide
" Fix <Enter> for comment
set fo+=cr
" buffers for autocomplete
set complete=""
set complete+=.
set complete+=k
set complete+=b
set complete+=t

set wildmenu
set wcm=<Tab>

"dont fold by default
set nofoldenable
" tags
set tags+=tags
set tags+=tags2
" runtimepath for vimdata
set runtimepath+=~/.vimdata
" vim-ruby
if has("unix")
  set runtimepath+=~/.vim/vim-ruby
  set runtimepath+=~/.vim/vim-rails
else
  set runtimepath+=~/vimfiles/vim-ruby
  set runtimepath+=~/vimfiles/vim-rails
endif

"-----------------------------------------------------------------------------
" hotkeys / shortcuts
"-----------------------------------------------------------------------------
vmap < <gv
vmap > >gv

" CTRL-X and SHIFT-Del are Cut
vnoremap <C-X> "+x
vnoremap <S-Del> "+x

" CTRL-C and CTRL-Insert are Copy
vnoremap <c-c> "+y
vnoremap <c-insert> "+y

" CTRL-V and SHIFT-Insert are Paste
"map <c-v>     "+gP
map <s-insert> "+gP

"cmap <c-v>    <c-r>+
cmap <s-insert> <c-r>+

" Pasting blockwise and linewise selections is not possible in Insert and
" Visual mode without the +virtualedit feature.  They are pasted as if they
" were characterwise instead.
" Uses the paste.vim autoload script.

exe 'inoremap <script> <C-V>' paste#paste_cmd['i']
exe 'vnoremap <script> <C-V>' paste#paste_cmd['v']

imap <s-insert>   <c-v>
vmap <s-insert>   <c-v>

nmap <a-v> <c-v>
vmap <a-v> <c-v>
"imap <c-v> <space><esc>"+gP<del>i
" movement
vmap <c-k> 10k
nmap <c-k> 10k
vmap <c-j> 10j
nmap <c-j> 10j
" up
nmap k gk
vmap k gk
imap <c-k> <up>
cmap <c-k> <up>
" down
nmap j gj
vmap j gj
imap <c-j> <down>
cmap <c-j> <down>
" left
nmap h <left>
vmap h <left>
imap <c-h> <left>
cmap <c-h> <left>
" right
nmap l <right>
vmap l <right>
imap <c-l> <right>
cmap <c-l> <right>
" ESC button
imap jj <Esc>
" arrows
nmap <down> gj
nmap <up> gk
vmap <down> gj
vmap <up> gk
imap <down> <c-O>gj
imap <up> <c-O>gk
" save position while page up/down
nmap <pageup> <c-U><c-U>
nmap <pagedown> <c-D><c-D>
imap <pageup> <c-O><c-U><c-O><c-U>
imap <pagedown> <c-O><c-D><c-O><c-D>
vmap <pageup> <c-U><c-U>
vmap <pagedown> <c-D><c-D>
" CamelCase movement
map <silent> w <plug>CamelCaseMotion_w
map <silent> b <plug>CamelCaseMotion_b
map <silent> e <plug>CamelCaseMotion_e
" Bubble single lines
nmap <c-up> [e
nmap <c-down> ]e
" Bubble multiple lines
vmap <c-up> [egv
vmap <c-down> ]egv
" move with shift
imap <s-k4> <esc>Bi
vmap <s-k4> B
nmap <s-k4> B
imap <s-k6> <esc>Ea
vmap <s-k6> E
nmap <s-k6> E
map <s-k8> <pageup>
map <s-k2> <pagedown>
" insert newline after current line
nmap <silent> <cr> o<Esc>
" insert newline before current line
nmap <silent> <s-cr> O<Esc>
" turn off highlighting and clear messages
nmap <silent> <space> :nohlsearch<Bar>:echo<cr>
" autocomplete
imap <tab> <c-r>=InsertTabWordWrapper()<cr>
imap <c-tab> <c-r>=InsertTabLineWrapper()<cr>
imap <s-tab> <c-n>

nmap <silent><a-S-left> :tabmove -1<cr>
imap <silent><a-S-left> <c-O>:tabmove -1<cr>
nmap <silent><a-S-right> :tabmove +1<cr>
imap <silent><a-S-right> <c-O>:tabmove +1<cr>
" search selected text
vmap <silent>* <esc>:call VisualSearch('/')<cr>/<c-R>/<cr>
vmap <silent># <esc>:call VisualSearch('?')<cr>?<c-R>/<cr>
" Trailing Spaces
nmap <silent>,t :call RemoveTrailingSpaces()<cr>:echo 'trailing spaces have been removed'<cr>
" Beginning of line
nmap <s-h> <c-0>eb
vmap <s-h> <c-0>eb
" End of line
nmap <s-l> $
vmap <s-l> $

nmap <f1> :CommandT<cr>
nmap ,<f1>r :CommandTFlush<cr>:CommandT<cr>
nmap <f2> :NERDTreeFind<cr>
nmap <f3> :BuffergatorToggle<cr>
let g:speckySpecSwitcherKey="<f4>"
" Tags
nnoremap <f12> :emenu Tags.<tab>
inoremap <f12> <c-O>:emenu Tags.<tab>
vnoremap <f12> <esc>:emenu Tags.<tab>
" ruby debugger
" vimrc edit
if exists('$MYGVIMRC')
  map ,v :vsp $MYGVIMRC<CR>
  map ,V :source $MYGVIMRC<CR>
else
  map ,v :vsp $MYVIMRC<CR>
  map ,V :source $MYVIMRC<CR>
end
"  'Control + \' - Open a new tab and tag into the function/variable currently under cursor
imap {<cr> {<cr>}<Esc>O
imap <% <%  %><left><left><left>
imap <%= <%= %><left><left><left>

map <Leader>f :call GrepIt()<cr>
" quotes replacement
nnoremap <silent><leader>'  :<C-U>call <SID>ToggleQuote()<CR>
nnoremap <silent><leader>"  :<C-U>call <SID>ToggleDoubleQuote()<CR>

map <c-\> :tab split<cr>:exec("tag ".expand("<cword>"))<cr>

"-------------------------------------------------------------------------------
" Generation of complementary brackets
"-------------------------------------------------------------------------------

"inoremap {{ {<Space><Space>}<Esc>hi
inoremap {{ {}<Esc>i
inoremap {{{ {{}}<Esc>hi
inoremap (( ()<Esc>i

" menu
"-------------------------------------------------------------------------------

if !has("unix")
  anoremenu &File.&Font.&Monaco_10 :set guifont=Monaco:h10<cr>
  anoremenu &File.&Font.&Consolas_10 :set guifont=Consolas:h12:cRUSSIAN<cr>
  anoremenu &File.&Font.&Monaco_12 :set guifont=Monaco:h10<cr>
  anoremenu &File.&Font.&Monaco_12_russian :set guifont=Monaco:h12:cRUSSIAN<cr>
endif

anoremenu &File.&EOL.&unix :setlocal fileformat=unix<cr>
anoremenu &File.&EOL.&dos :setlocal fileformat=dos<cr>
anoremenu &File.&EOL.&mac :setlocal fileformat=mac<cr>

anoremenu &File.E&ncoding.&Write.&utf-8 :setlocal fileencoding=utf-8<cr>
anoremenu &File.E&ncoding.&Write.&windows-1251 :setlocal fileencoding=windows-1251<cr>
anoremenu &File.E&ncoding.&Write.&iso-8859-15 :setlocal fileencoding=iso-8859-15<cr>
anoremenu &File.E&ncoding.&Write.&koi8-r :setlocal fileencoding=koi8-r<cr>

anoremenu &File.E&ncoding.&Read.&utf-8 :edit ++enc=utf-8<cr>
anoremenu &File.E&ncoding.&Read.&windows-1251 :edit ++enc=windows-1251<cr>
anoremenu &File.E&ncoding.&Read.&iso-8859-15 :edit ++enc=iso-8859-15<cr>
anoremenu &File.E&ncoding.&Read.&koi8-r :edit ++enc=koi8-r<cr>

anoremenu &File.&Spell.&Combined :setlocal spell spelllang=ru,en<cr>
anoremenu &File.&Spell.&Russian :setlocal spell spelllang=ru<cr>
anoremenu &File.&Spell.&English :setlocal spell spelllang=en<cr>
anoremenu &File.&Spell.&Off :setlocal nospell spelllang=<cr>

anoremenu &File.&Preview.&Firefox :!firefox %<cr>
anoremenu &File.&Preview.&Opera :!opera %<cr>
anoremenu &File.&Preview.&Midori :!midori %<cr>

" tag list
anoremenu &Tags.&Tagbar :TagbarToggle<cr>
anoremenu &Tags.&TagList :TlistToggle<cr>
anoremenu &Tags.&ctags :exec("!ctags -R --extra=+q --exclude=log --exclude=vendor --exclude=tmp --exclude=public --exclude=.git --exclude=.env --exclude=.rspec .")<cr>
if has("unix")
  anoremenu &Tags.&rjstags :exec("!ruby ~/.vim/bin/rjstags/rjstags.rb .")<cr>
  anoremenu &Tags.&rjstags\ file :exec("!ruby ~/.vim/bin/rjstags/rjstags.rb %")<cr>
else
  anoremenu &Tags.&rjstags :exec("!ruby '".$HOME."//vimfiles//bin//rjstags//rjstags.rb' .")<cr>
  anoremenu &Tags.&rjstags\ file :exec("!ruby '".$HOME."//vimfiles//bin//rjstags//rjstags.rb' %")<cr>
endif
"-----------------------------------------------------------------------------
" autocommands
"-----------------------------------------------------------------------------
"nmap <LocalLeader>k :exec("!lynx -accept_all_cookies http://php.net/".expand("<cword>"))<cr>
"command! -nargs=0 RDocPreview call RDocRenderBufferToPreview()

"function! RDocRenderBufferToPreview()
"  let rdocoutput = "/tmp/vimrdoc/"
"  call system("rdoc " . bufname("%") . " --op " . rdocoutput)
"  call system("open -a Safari ". rdocoutput . "index.html")
"endfunction

" backups
autocmd! bufwritepre * call BackupDir()
" reload vimrc
if has("unix")
  autocmd! bufwritepost ~/.vimrc source ~/.vimrc
  autocmd! bufwritepost ~/.gvimrc source ~/.gvimrc

  autocmd! bufwritepost ~/.vim/vimrc source ~/.vim/vimrc
  autocmd! bufwritepost ~/.vim/gvimrc source ~/.vim/gvimrc
else
  autocmd! bufwritepost ~/vimfiles/gvimrc source ~/vimfiles/gvimrc
  autocmd! bufwritepost ~/vimfiles/vimrc source ~/vimfiles/vimrc
  autocmd! bufwritepost $VIM/_gvimrc source $VIM/_gvimrc
  autocmd! bufwritepost $VIM/_vimrc source $VIM/_vimrc
endif

"-------------------------------------------------------------------------------
" File Types
"-------------------------------------------------------------------------------
au BufRead,BufNewFile *.scss set filetype=scss
au BufRead,BufNewFile *_spec.rb set filetype=rspec
au BufNewFile,BufRead *.json set filetype=javascript
au BufNewFile,BufRead *.slim set filetype=slim
au BufNewFile,BufRead *.json set filetype=json
au BufNewFile,BufRead *.json.jb set filetype=ruby
au BufNewFile,BufRead *.rb set makeprg=ruby\ -c\ %
au BufNewFile,BufRead *.ass,*.ssa set filetype=ssa
" syntax highlight fix for brackets
au BufNewFile,BufRead * syn match rubyParens "[()\[\]]"
au FileType go set nolist

"-------------------------------------------------------------------------------
" omni completion
"-------------------------------------------------------------------------------
autocmd FileType ruby set omnifunc=rubycomplete#Complete
autocmd FileType python set omnifunc=pythoncomplete#Complete
autocmd FileType javascript set omnifunc=RjsComplete
autocmd FileType html set omnifunc=htmlcomplete#CompleteTags
autocmd FileType css set omnifunc=csscomplete#CompleteCSS
autocmd FileType xml set omnifunc=xmlcomplete#CompleteTags
autocmd FileType php set omnifunc=phpcomplete#CompletePHP
autocmd FileType c set omnifunc=ccomplete#Complete

"-----------------------------------------------------------------------------
" functions
"-----------------------------------------------------------------------------
function! GrepIt()
  let l:word = expand("<cword>")
  echo 'Searching for "'.l:word.'"...'
  exec('Grep '.l:word.' **/*')
endfunction

function! VisualSearch(cmd)
  let l:old_reg=getreg('"')
  let l:old_regtype=getregtype('"')
  normal! gvy
  let @/='\V'.substitute(substitute(substitute(escape(@@, a:cmd.'\'), '^\_s\+', '\\s\\+', ''), '\_s\+$', '\\s\\*', ''), '\_s\+', '\\_s\\+', 'g')
  normal! gV
  call setreg('"', l:old_reg, l:old_regtype)
endfunction

function! RemoveTrailingSpaces()
  normal! mzHmy
"  execute '%s/^        /  /ge'
"  execute '%s/ /  /ge'
  execute '%s/\t/  /ge'
  execute '%s/\s\+$//ge'
  normal! 'yzt`z
endfunction

function! BackupDir()
  if has("win32")
    let l:backupdir=$HOME.'\.backup\'.substitute(expand('%:p:h'), ':', '', '')
  else
    let l:backupdir=substitute($HOME.'/.backup/'.substitute(expand('%:p:h'), ':', '', ''), '//', '/', '')
  endif

  if !isdirectory(l:backupdir)
    call mkdir(l:backupdir, 'p', 0700)
  endif

  let &backupdir=l:backupdir

  if has("win32")
    let &backupext=strftime('~%Y-%m-%d %H_%M_%S~')
  else
    let &backupext=strftime('~%Y-%m-%d %H:%M:%S~')
  endif
endfunction

function! InsertTabWordWrapper()
 let col = col('.') - 1
 if !col || getline('.')[col - 1] !~ '\k'
   return "\<tab>"
 else
   return "\<c-p>"
 endif
endfunction

function! InsertTabLineWrapper()
 let col = col('.') - 1
 if !col || getline('.')[col - 1] !~ '\k'
   return "\<tab>"
 else
   return "\<c-x>\<c-l>"
 endif
endfunction

function! InsertSnippetWrapper()
 let inserted = TriggerSnippet()
 if inserted == "\<tab>"
   return ";"
 else
   return inserted
 endif
endfunction

autocmd cursorhold,bufwritepost * unlet! b:statusline_trailing_space_warning

"return '[\s]' if trailing white space is detected
"return '' otherwise
function! StatuslineTrailingSpaceWarning()
    if !exists("b:statusline_trailing_space_warning")
        if search('\s\+$', 'nw') != 0
            let b:statusline_trailing_space_warning = '[\s]'
        else
            let b:statusline_trailing_space_warning = ''
        endif
    endif
    return b:statusline_trailing_space_warning
endfunction


"return the syntax highlight group under the cursor ''
function! StatuslineCurrentHighlight()
    let name = synIDattr(synID(line('.'),col('.'),1),'name')
    if name == ''
        return ''
    else
        return '[' . name . ']'
    endif
endfunction

"recalculate the tab warning flag when idle and after writing
autocmd cursorhold,bufwritepost * unlet! b:statusline_tab_warning

"return '[&et]' if &et is set wrong
"return '[mixed-indenting]' if spaces and tabs are used to indent
"return an empty string if everything is fine
function! StatuslineTabWarning()
    if !exists("b:statusline_tab_warning")
        let tabs = search('^\t', 'nw') != 0
        let spaces = search('^ ', 'nw') != 0

        if tabs && spaces
            let b:statusline_tab_warning =  '[mixed-indenting]'
        elseif (spaces && !&et) || (tabs && &et)
            let b:statusline_tab_warning = '[&et]'
        else
            let b:statusline_tab_warning = ''
        endif
    endif
    return b:statusline_tab_warning
endfunction

"recalculate the long line warning when idle and after saving
autocmd cursorhold,bufwritepost * unlet! b:statusline_long_line_warning

"return a warning for "long lines" where "long" is either &textwidth or 80 (if
"no &textwidth is set)
"
"return '' if no long lines
"return '[#x,my,$z] if long lines are found, were x is the number of long
"lines, y is the median length of the long lines and z is the length of the
"longest line
function! StatuslineLongLineWarning()
    if !exists("b:statusline_long_line_warning")
        let long_line_lens = s:LongLines()

        if len(long_line_lens) > 0
            let b:statusline_long_line_warning = "[" .
                        \ '#' . len(long_line_lens) . "," .
                        \ 'm' . s:Median(long_line_lens) . "," .
                        \ '$' . max(long_line_lens) . "]"
        else
            let b:statusline_long_line_warning = ""
        endif
    endif
    return b:statusline_long_line_warning
endfunction

"return a list containing the lengths of the long lines in this buffer
function! s:LongLines()
    let threshold = (&tw ? &tw : 80)
    let spaces = repeat(" ", &ts)

    let long_line_lens = []

    let i = 1
    while i <= line("$")
        let len = strlen(substitute(getline(i), '\t', spaces, 'g'))
        if len > threshold
            call add(long_line_lens, len)
        endif
        let i += 1
    endwhile

    return long_line_lens
endfunction

"find the median of the given array of numbers
function! s:Median(nums)
    let nums = sort(a:nums)
    let l = len(nums)

    if l % 2 == 1
        let i = (l-1) / 2
        return nums[i]
    else
        return (nums[l/2] + nums[(l/2)-1]) / 2
    endif
endfunction

"visual search mappings
function! s:VSetSearch()
    let temp = @@
    norm! gvy
    let @/ = '\V' . substitute(escape(@@, '\'), '\n', '\\n', 'g')
    let @@ = temp
endfunction
vnoremap * :<C-u>call <SID>VSetSearch()<CR>//<CR>
vnoremap # :<C-u>call <SID>VSetSearch()<CR>??<CR>

" The idea is to delete something anywhere, then go select characters elsewhere, and hit g" to swap the delete characters with the newly selected ones.
function! s:SwapVisualWithCut()
  normal! `.``
  if line(".")==line("'.") && col(".") < col("'.")
    let c = col('.')
    normal! gvp```]
    let c = col('.') - c
    normal! ``
    :silent call cursor(line("."),col(".")+c)
    normal! P
  else
    normal! gvp``P
  endif
endfunction
vnoremap <silent> g" <esc>:call <sid>SwapVisualWithCut()<cr>

"jump to last cursor position when opening a file
"dont do it when writing a commit log entry
autocmd BufReadPost * call SetCursorPosition()
function! SetCursorPosition()
    if &filetype !~ 'commit\c'
        if line("'\"") > 0 && line("'\"") <= line("$")
            exe "normal! g`\""
            normal! zz
        endif
    end
endfunction

"define :HighlightLongLines command to highlight the offending parts of
"lines that are longer than the specified length (defaulting to 80)
command! -nargs=? HighlightLongLines call s:HighlightLongLines('<args>')
function! s:HighlightLongLines(width)
    let targetWidth = a:width != '' ? a:width : 79
    if targetWidth > 0
        exec 'match Todo /\%>' . (targetWidth) . 'v/'
    else
        echomsg "Usage: HighlightLongLines [natural number]"
    endif
endfunction

function! ToggleMatchParen()
  if exists("g:loaded_matchparen")
    :NoMatchParen
    :echo 'MatchParen plugin turned off'
  else
    :DoMatchParen
    :echo 'MatchParen plugin turned on'
  endif
endfunction

function! EnableDebugger()
  Rdebugger
  nnoremap <f5> :Rdebugger %<cr>
  inoremap <f5> <c-O>:Rdebugger %<cr>
  vnoremap <f5> <esc>:Rdebugger %<cr>
  nnoremap <s-f5> :call g:RubyDebugger.exit()<cr>
  inoremap <s-f5> <c-O>:call g:RubyDebugger.exit()<cr>
  vnoremap <s-f5> <esc>:call g:RubyDebugger.exit()<cr>
  nnoremap <f8> :call g:RubyDebugger.continue()<cr>
  inoremap <f8> <c-O>:call g:RubyDebugger.continue()<cr>
  vnoremap <f8> <esc>:call g:RubyDebugger.continue()<cr>
  nnoremap <f9> :call g:RubyDebugger.toggle_breakpoint()<cr>
  inoremap <f9> <c-O>:call g:RubyDebugger.toggle_breakpoint()<cr>
  vnoremap <f9> <esc>:call g:RubyDebugger.toggle_breakpoint()<cr>
  nnoremap <f10> :call g:RubyDebugger.next()<cr>
  inoremap <f10> <c-O>:call g:RubyDebugger.next()<cr>
  vnoremap <f10> <esc>:call g:RubyDebugger.next()<cr>
  nnoremap <f11> :call g:RubyDebugger.step()<cr>
  inoremap <f11> <c-O>:call g:RubyDebugger.step()<cr>
  vnoremap <f11> <esc>:call g:RubyDebugger.step()<cr>
  nnoremap <s-f11> :call g:RubyDebugger.finish()<cr>
  inoremap <s-f11> <c-O>:call g:RubyDebugger.finish()<cr>
  vnoremap <s-f11> <esc>:call g:RubyDebugger.finish()<cr>
endfunction

" replacement for quotes
function! s:ToggleQuote()
    let q = searchpos("'", 'n', line('.'))
    let qb = searchpos("'", 'bn', line('.'))
    let dq = searchpos('"', 'n', line('.'))
    let dqb = searchpos('"', 'bn', line('.'))

    if q[0] > 0 && qb[0] > 0 && (dq[0] == 0 || dq[0] > q[0])
        execute "normal mzcs'\"`z"
    elseif dq[0] > 0 && dqb[0] > 0
        execute "normal mzcs\"'`z"
    endif
endfunction

" replacement for double quotes
function! s:ToggleDoubleQuote()
    let q = searchpos('"', 'n', line('.'))
    let qb = searchpos('"', 'bn', line('.'))
    let dq = searchpos("'", 'n', line('.'))
    let dqb = searchpos("'", 'bn', line('.'))

    if q[0] > 0 && qb[0] > 0 && (dq[0] == 0 || dq[0] > q[0])
        execute "normal mzcs\"'`z"
    elseif dq[0] > 0 && dqb[0] > 0
        execute "normal mzcs'\"`z"
    endif
endfunction

"-----------------------------------------------------------------------------
" plugins
"-----------------------------------------------------------------------------
" ruby code completion
let g:rubycomplete_buffer_loading = 1
let g:rubycomplete_classes_in_global = 1
let g:rubycomplete_include_object = 1
let g:rubycomplete_include_objectspace = 1
let g:rubycomplete_rails = 1

" SessionMgr
let g:SessionMgr_AutoManage = 0
let g:SessionMgr_DefaultName = "last"
" SnipMate
"let s:did_snips_mappings = 1
" Tlist settings
let g:Tlist_Show_One_File = 1
let g:Tlist_GainFocus_On_ToggleOpen = 1
let g:Tlist_Use_Right_Window = 1
let g:Tlist_WinWidth = 45
set completeopt-=preview
set completeopt+=longest

" javascript omnicomplete
let g:rjscomplete_library = 'jQuery_1.4'
let g:rjscomplete_find_in_prototype = 0

" used to make vim default 'man' viewer
" see http://vim.wikia.com/wiki/Using_vim_as_a_man-page_viewer_under_Unix
let $PAGER=''
